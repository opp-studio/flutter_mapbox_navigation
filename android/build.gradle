group 'com.eopeter.flutter_mapbox_navigation'
version '1.0'

buildscript {
    ext.kotlin_version = '1.9.22'
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = 'mapbox'
                password = project.hasProperty('MAPBOX_DOWNLOADS_TOKEN') 
                    ? project.property('MAPBOX_DOWNLOADS_TOKEN') 
                    : System.getenv('MAPBOX_DOWNLOADS_TOKEN')
                if (password == null || password == "") {
                    throw new Exception("MAPBOX_DOWNLOADS_TOKEN isn't set. Set it to the project properties or to the environment variables.")
                }
            }
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = 'mapbox'
                password = project.hasProperty('MAPBOX_DOWNLOADS_TOKEN') 
                    ? project.property('MAPBOX_DOWNLOADS_TOKEN') 
                    : System.getenv('MAPBOX_DOWNLOADS_TOKEN')
                if (password == null || password == "") {
                    throw new Exception("MAPBOX_DOWNLOADS_TOKEN isn't set. Set it to the project properties or to the environment variables.")
                }
            }
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 36
    ndkVersion "27.0.12077973"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
        
        ndk {
            abiFilters 'arm64-v8a', 'x86_64'  // Only 64-bit architectures for 16KB compliance
        }
        
        // 16 KB page size support - NDK 27 configuration
        // Note: This plugin is Kotlin-only, no native C++ compilation needed
    }
    
    buildFeatures {
        dataBinding false
        viewBinding false
    }
    
    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
    }
    
    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
            pickFirsts += ['**/libc++_shared.so', '**/libjsc.so']
            excludes += ['META-INF/*.kotlin_module']
        }
    }
    
    // 16KB page size alignment for all native libraries
    // Note: This is a library module, not an application
    
    namespace 'com.eopeter.fluttermapboxnavigation'
    
    // 16 KB page size support - No native build needed for Kotlin-only plugin
}

configurations.all {
    resolutionStrategy {
        force 'com.mapbox.common:common-ndk27:24.16.0'
        exclude group: 'com.mapbox.common', module: 'okhttp-ndk27'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    
    // Mapbox Navigation SDK and Maps SDK with NDK 27 support for 16 KB page size
    implementation('com.mapbox.navigation:android-ndk27:2.21.0')
    implementation('com.mapbox.navigation:ui-dropin-ndk27:2.21.0')
    implementation('com.mapbox.navigation:ui-maps-ndk27:2.21.0')
    implementation('com.mapbox.maps:android-ndk27:11.16.0')
    
    // AndroidX dependencies
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.11.0'
    
    // Lifecycle components
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.0.0'
}